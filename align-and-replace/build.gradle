plugins {
    id 'java'
}

repositories {
    mavenCentral()
    ivy { url 'testrepogen/ivyrepo' }
}

dependencies {
    implementation 'com.amazon:aws-java-sdk:1.3.11'
    implementation 'com.amazonaws:aws-java-sdk-autoscaling:1.11.521'
    implementation 'com.amazonaws:aws-java-sdk-cloudwatch:1.11.702'
}

project.dependencies.modules.module('com.amazon:aws-java-sdk') {
    def details = it as ComponentModuleMetadataDetails
    def message = "âœ­ replacement com.amazon:aws-java-sdk -> com.amazonaws:aws-java-sdk"
    details.replacedBy('com.amazonaws:aws-java-sdk', message)
}

project.dependencies.components.all(AlignGroup.class)

class AlignGroup implements ComponentMetadataRule {
    void execute(ComponentMetadataContext ctx) {
        ctx.details.with { it ->
            if (it.getId().getGroup().startsWith("com.amazonaws")) {
                it.belongsTo("com.amazonaws:com.amazonaws:${it.getId().getVersion()}")
            }
        }
    }
}
