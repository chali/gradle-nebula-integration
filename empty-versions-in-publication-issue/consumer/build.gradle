plugins {
    id 'java-library'
    id 'maven-publish'
}

repositories {
    jcenter()
}


dependencies {
    api platform(project(':platform'))
    implementation 'com.google.collections:google-collections' // no version since is coming from platform
    implementation 'com.google.guava:guava:latest.release'
    modules {
        module("com.google.collections:google-collections") {
            replacedBy("com.google.guava:guava", "google-collections is now part of Guava")
        }
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'org.gradle.sample'
            artifactId = 'project1-sample'
            version = '1.1'
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
        }

    }
}
